apply plugin: 'groovy'
apply plugin: 'idea'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.6'
    compile 'commons-cli:commons-cli:1.3.1'
    testCompile 'junit:junit:4.10'
    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
}

task createSourceDirs(description : 'Create empty source directories for all defined sourceSets') << {
    sourceSets*.allSource.srcDirs.flatten().each { File sourceDirectory ->
        if (!sourceDirectory.exists()) {
            println "Making $sourceDirectory"
            sourceDirectory.mkdirs()
        }
    }
}

idea {
    project {
        jdkName = '1.6'
    }
}


task syncWithRepo(dependsOn: 'classes', type: JavaExec) {
    main = 'com.entagen.jenkins.Main'
    classpath = sourceSets.main.runtimeClasspath
    // pass through specified system properties to the call to main
    ['help', 'jenkinsUrl', 'jenkinsUser', 'jenkinsPassword', 'gitUrl', 'templateJobPrefix', 'templateBranchName', 'branchNameRegex', 'nestedView', 'viewRegex', 'printConfig', 'dryRun', 'startOnCreate', 'noViews', 'noDelete', 'enableJob'].each {
        if (System.getProperty(it)) systemProperty it, System.getProperty(it)
    }

}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}






